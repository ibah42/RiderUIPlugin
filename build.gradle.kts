import org.jetbrains.intellij.platform.gradle.TestFrameworkType

plugins {
    id("java")
    id("org.jetbrains.kotlin.jvm") version "2.4.20"
    id("org.jetbrains.intellij.platform") version "2.19.0"
}

group = "com.hitapps"
version = "1.8.0"

repositories {
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    intellijPlatform {
        create(
            providers.gradleProperty("platformType"),
            providers.gradleProperty("platformVersion"),
        )
        testFramework(TestFrameworkType.Platform)
    }
    // kotlin-stdlib is deliberately not declared: the IDE ships its own
    // (see kotlin.stdlib.default.dependency=false in gradle.properties).
    testImplementation("junit:junit:4.13.2")
}

kotlin {
    // The 2026.x platform runs on JBR 21, so target that rather than the JDK running Gradle.
    jvmToolchain(21)
}

intellijPlatform {
    pluginConfiguration {
        ideaVersion {
            sinceBuild = providers.gradleProperty("pluginSinceBuild")
            // untilBuild is deliberately left unset: the default comes from the target build.
            // When the next Rider ships, raise platformVersion and rebuild.
        }
    }
    buildSearchableOptions = false
}

tasks.test {
    useJUnit()
    testLogging {
        events("passed", "failed", "skipped")
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
    }
}
